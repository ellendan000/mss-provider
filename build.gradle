buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/release/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
//        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'pmd'
apply plugin: 'jacoco'
//apply plugin: 'org.sonarqube'

sourceCompatibility = 1.8
targetCompatibility = 1.8

pmd {
    toolVersion = "6.8.0"
    ruleSetFiles = files('config/pmd-ruleSets.xml')
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release/' }
}

ext {
    lib = [
            springBoot: [
                    'org.springframework.boot:spring-boot-starter-web',
                    'org.springframework.boot:spring-boot-starter-data-jpa',
                    'org.springframework.boot:spring-boot-starter-validation',
                    'org.springframework.boot:spring-boot-starter-actuator',
                    'org.springframework.security:spring-security-web',
                    'org.springframework.security:spring-security-config'
            ],
            springCloud : [
                    'org.springframework.cloud:spring-cloud-starter-openfeign',
                    'org.springframework.cloud:spring-cloud-starter-netflix-hystrix',
                    'org.springframework.cloud:spring-cloud-starter-netflix-hystrix-dashboard',
                    'io.github.openfeign.form:feign-form:3.5.0',
            ],
            db        : [
                    'mysql:mysql-connector-java',
                    'org.flywaydb:flyway-core:5.2.4'
            ],
            commons   : [
                    'commons-lang:commons-lang:2.6',
                    'commons-io:commons-io:2.6'
            ],
            utils     : [
                    'com.google.guava:guava:27.0.1-jre',
                    'org.modelmapper:modelmapper:2.3.0'
            ],
            basicTest : [
                    'org.junit.jupiter:junit-jupiter-api',
                    'org.junit.jupiter:junit-jupiter-params',
                    'org.junit.jupiter:junit-jupiter-engine',
                    "org.mockito:mockito-core:${mokitoVersion}",
                    "org.mockito:mockito-junit-jupiter:${mokitoVersion}",
            ],
            integTest : [
                    'org.springframework.boot:spring-boot-starter-test',
                    'io.rest-assured:rest-assured:3.2.0',
                    'io.rest-assured:spring-mock-mvc:3.2.0'
            ],
            dbTest    : [
                    'com.h2database:h2:1.4.199'
            ],
            swagger   : [
                    'io.springfox:springfox-swagger2:2.9.2',
                    'io.springfox:springfox-swagger-ui:2.9.2'
            ]
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.junit:junit-bom:${junitVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation lib.springBoot, lib.springCloud, lib.db, lib.utils, lib.commons, lib.swagger
    testImplementation lib.basicTest, lib.integTest, lib.dbTest
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}
test.finalizedBy jacocoTestCoverageVerification, jacocoTestReport
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }

        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }
    }
}